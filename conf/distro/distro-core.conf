# DISTRO.conf

# Used to define DISTRO with GUI

# Aspects of build-in inclusion are denoted with the use of an OVERRIDE from
# DISTROOVERRIDES. Where functionality is only available from a specific Yocto
# meta-* layer, incorporate the appropriate conditional checks and test with
# that layer absent.

# OVERRIDES                 Description
# none                      default required even if empty
# :${DISTRO}                additions based on DISTRO
# :${DISTRO_CODENAME}       additions based on DISTRO_CODENAME
# :distro-guest             additions for virtual machine guests
# :distro-gui               additions for gnome, xfce, wayland, x11
# :distro-kubernetes        additions for kubernetes, k3s
# :distro-kvm               additions for kvm, lxc, libvirt
# :distro-testfw            additions for test frameworks
# :libc-glibc               additions for runtime glibc, locales
# :libc-musl                additions for runtime musl
# :<distrooverride>         additions for DISTROOVERRIDES

DISTROOVERRIDES =. "${DISTRO_CODENAME}:"
DISTROOVERRIDES =. "distro-gui:"

# Inherit from DISTRO poky
TCLIBC = "glibc"
TCLIBCAPPEND = "-${TCLIBC}"
require conf/distro/poky.conf

DISTRO = "distro-core"
require conf/distro/include/distro-version.inc

DISTRO_NAME = "DISTRO Core (${DISTRO})"
MAINTAINER = "RPM Signing <release@internal>"

TARGET_VENDOR = "-distro"

SDK_VENDOR = "-distro${TCLIBC}"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

SDKMACHINE ?= "x86_64"

# selects "mdev-busybox", "systemd", "sysvinit"
INIT_MANAGER = "systemd"

# features
DISTRO_FEATURES:append = " \
integrity security virtualization sota usrmerge ${INIT_MANAGER} \
acl audit efi overlayfs pam pci usbgadget usbhost \
tpm tpm2 wifi bluetooth polkit selinux \
ipsec ipv4 ipv6 luks seccomp \
"

DISTRO_FEATURES:append = " \
opengl pulseaudio vulkan wayland x11 \
"

DISTRO_FEATURES:append:distro-testfw = " \
ptest \
"

DISTRO_FEATURES:remove = " \
3g api-documentation ima nfc nfs pcmcia zeroconf smbfs \
modsign \
"

DISTRO_EXTRA_RDEPENDS:append = ""

DISTRO_FEATURES:append:class-native = " usrmerge ${INIT_MANAGER}"

# ---
